// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN
        x[16], y[16],  // 16-bit inputs
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0


    PARTS:
    // --- X VALUE
    Not16(in=x, out=inversedX);
    And16(a=x, b=inversedX, out=zeroX);
    Mux16(a=x, b=zeroX, sel=zx, out=zxVal);

    Not16(in=zxVal, out=inversedZXVal);
    Mux16(a=zxVal, b=inversedZXVal, sel=nx, out=nxVal);
    // --- Y VALUE
    Not16(in=y, out=inversedY);
    And16(a=y, b=inversedY, out=zeroY);
    Mux16(a=y, b=zeroY, sel=zy, out=zyVal);

    Not16(in=zyVal, out=inversedZYVal);
    Mux16(a=zyVal, b=inversedZYVal, sel=ny, out=nyVal);
    // --- F
    Add16(a=nxVal, b=nyVal, out=addF);
    And16(a=nxVal, b=nyVal, out=andF);
    Mux16(a=andF, b=addF, sel=f, out=fVal);
    // --- NO
    Not16(in=fVal, out=negatedFVal);
    Mux16(a=fVal, b=negatedFVal, sel=no, out=out, out[15]=ng, out[0..7]=lowBitOut, out[8..15]=highBitOut);
    // --- ZR
    Or8Way(in=lowBitOut, out=lowBitVal);
    Or8Way(in=highBitOut, out=highBitVal);
    Or(a=lowBitVal, b=highBitVal, out=ngZr);
    Not(in=ngZr, out=zr);
}